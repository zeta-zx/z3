#define REFLECT
#define WARP_WORLD
#define SKYBOX1

//#define BACKSTEP


#define TIME        iTime
#define RESOLUTION  iResolution
#define PI          3.141592654
#define TAU         (2.0*PI)

#define MAX_RAY_LENGTH_HI   48.0
#define TOLERANCE_HI        0.0001
#define MAX_RAY_MARCHES_HI  70
#define NORM_OFF            0.001
#define ROT(a)              mat2(cos(a), sin(a), -sin(a), cos(a))

#define SCA(a)              vec2(sin(a), cos(a))

// License: WTFPL, author: sam hocevar, found: https://stackoverflow.com/a/17897228/418488
const vec4 hsv2rgb_K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
vec3 hsv2rgb(vec3 c) {
  vec3 p = abs(fract(c.xxx + hsv2rgb_K.xyz) * 6.0 - hsv2rgb_K.www);
  return c.z * mix(hsv2rgb_K.xxx, clamp(p - hsv2rgb_K.xxx, 0.0, 1.0), c.y);
}
// License: WTFPL, author: sam hocevar, found: https://stackoverflow.com/a/17897228/418488
//  Macro version of above to enable compile-time constants
#define HSV2RGB(c)  (c.z * mix(hsv2rgb_K.xxx, clamp(abs(fract(c.xxx + hsv2rgb_K.xyz) * 6.0 - hsv2rgb_K.www) - hsv2rgb_K.xxx, 0.0, 1.0), c.y))

#if defined(WARP_WORLD)
#define PATHA (0.33*vec2(0.1147, 0.2093))
#define PATHB (0.33*vec2(13.0, 3.0))
vec3 path(float z) {
  return vec3(sin(z*PATHA)*PATHB, z);
}

vec3 dpath(float z) {
  return vec3(PATHA*PATHB*cos(PATHA*z), 1.0);
}

vec3 ddpath(float z) {
  return vec3(-PATHA*PATHA*PATHB*sin(PATHA*z), 0.0);
}
#endif

float beat() {
 float beat = 0.0;
  beat = texture(iChannel0, vec2(0.75, 0.25)).x;
  beat -= 0.25;
  beat *= beat;
  beat *= beat;
  beat *= 100.0;
  beat = clamp(beat, 0.0, 1.0);
  return beat;
}

// License: MIT OR CC-BY-NC-4.0, author: mercury, found: https://mercury.sexy/hg_sdf/
float mod1(inout float p, float size) {
  float halfsize = size*0.5;
  float c = floor((p + halfsize)/size);
  p = mod(p + halfsize, size) - halfsize;
  return c;
}

// License: MIT, author: Inigo Quilez, found: https://www.iquilezles.org/www/articles/smin/smin.htm
float pmin(float a, float b, float k) {
  float h = clamp(0.5+0.5*(b-a)/k, 0.0, 1.0);
  return mix(b, a, h) - k*h*(1.0-h);
}

// License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
float pmax(float a, float b, float k) {
  return -pmin(-a, -b, k);
}

float rayPlane(vec3 ro, vec3 rd, vec4 p) {
  return -(dot(ro,p.xyz)+p.w)/dot(rd,p.xyz);
}

// License: MIT, author: Inigo Quilez, found: https://iquilezles.org/articles/distfunctions2d/
float flatTorus(vec3 p, vec2 dim) {
  float d = length(p.xy)-dim.x;
  d = abs(d) - dim.y;
  vec2 w = vec2(d, abs(p.z) - dim.y);
  return min(max(w.x,w.y),0.0) + length(max(w,0.0));
}

float cappedTorus(vec3 p, vec2 sc, vec2 t) {
  float ra = t.x;
  float rb = t.y;
  p.x = abs(p.x);
  float k = (sc.y*p.x>sc.x*p.y) ? dot(p.xy,sc) : length(p.xy);
  return sqrt( dot(p,p) + ra*ra - 2.0*ra*k ) - rb;
}

float arc(vec2 p, vec2 sc, float ra, float rb) {
  // sc is the sin/cos of the arc's aperture
  p.x = abs(p.x);
  return ((sc.y*p.x>sc.x*p.y) ? length(p-sc*ra) : 
                                abs(length(p)-ra)) - rb;
}

#if defined(SKYBOX0)
// License: MIT, author: Inigo Quilez, found: https://iquilezles.org/www/articles/distfunctions2d/distfunctions2d.htm
float box(vec2 p, vec2 b) {
  vec2 d = abs(p)-b;
  return length(max(d,0.0)) + min(max(d.x,d.y),0.0);
}

const float hoff      = 0.0;
const vec3 skyCol     = HSV2RGB(vec3(hoff+0.57, 0.70, 0.25));
const vec3 glowCol0   = HSV2RGB(vec3(hoff+0.4, 0.85, 0.00125));
const vec3 glowCol1   = HSV2RGB(vec3(hoff+0.55, 0.85, 0.05));
const vec3 sunCol1    = HSV2RGB(vec3(hoff+0.60, 0.50, 0.5));
const vec3 sunCol2    = HSV2RGB(vec3(hoff+0.05, 0.75, 25.0));
const vec3 diffCol    = HSV2RGB(vec3(hoff+0.45, 0.5, 0.25));
const vec3 sunDir     = normalize(vec3(0., 0.5, 7.0));

const vec3 sunCol     = sunCol1;

vec3 render0(vec3 ro, vec3 rd, vec3 nrd, float beat) {
  vec3 col = vec3(0.0);
  float sd = max(dot(sunDir, rd), 0.0);
  float sf = 1.0001-sd;


  col += clamp(vec3(1.0/abs(rd.y))*glowCol0, 0.0, 1.0);
  col += 0.75*skyCol*pow((1.0-abs(rd.y)), 8.0);
  col += 2.0*sunCol1*pow(sd, 100.0);
  col += sunCol2*pow(sd, 800.0);

  float tp1  = rayPlane(ro, rd, vec4(vec3(0.0, -1.0, 0.0), -6.0));

  if (tp1 > 0.0) {
    vec3 pos  = ro + tp1*rd;
    vec2 pp = pos.xz;
    float db = box(pp, vec2(5.0, 9.0))-3.0;
    
    col += vec3(4.0)*skyCol*rd.y*rd.y*smoothstep(0.25, 0.0, db);
    col += vec3(0.8)*skyCol*exp(-0.5*max(db, 0.0));
    col += 0.25*sqrt(skyCol)*max(-db, 0.0);
  }

  return clamp(col, 0.0, 10.0);
}
#elif defined(SKYBOX1)
#define ROTY(a)               \
  mat3(                       \
    +cos(a) , 0.0 , +sin(a) \
  , 0.0     , 1.0 , 0.0     \
  , -sin(a) , 0.0 , +cos(a) \
  )

#define ROTZ(a)               \
  mat3(                       \
    +cos(a) , +sin(a) , 0.0   \
  , -sin(a) , +cos(a) , 0.0   \
  , 0.0     , 0.0     , 1.0   \
  )

#define ROTX(a)               \
  mat3(                       \
    1.0 , 0.0     , 0.0       \
  , 0.0 , +cos(a) , +sin(a)   \
  , 0.0 , -sin(a) , +cos(a)   \
  )

const mat3 roty       = ROTY(radians(10.0));
const vec3 sunDir     = normalize(vec3(0.0, -0.01, 1.0))*roty;
const vec3 lightPos   = vec3(0.0, -60.0, -200.0)*roty;
const float hoff      = 0.725;
const vec3 sunColor   = HSV2RGB(vec3(hoff+0.0, 0.9, 0.0005));
const vec3 topColor   = HSV2RGB(vec3(hoff+0.0, 0.9, 0.0001));
const vec3 glowColor0 = HSV2RGB(vec3(hoff+0.0, 0.9, 0.0001));
const vec3 glowColor2 = HSV2RGB(vec3(hoff+0.3, 0.95, 0.001));
const vec3 diffColor  = HSV2RGB(vec3(hoff+0.0, 0.9, .25));

const vec3 glowCol1   = HSV2RGB(vec3(hoff+0.2, 0.85, 0.0125));
const vec3 diffCol    = diffColor;
const vec3 sunCol     = sunColor;

vec2 planeCoord(vec3 p, vec3 c, vec3 up, vec4 dim) {
  vec3 d = p - c;
  vec3 xx = (cross(up,dim.xyz));
  vec3 yy = (cross(xx,dim.xyz));
  return vec2(dot(d,xx), dot(d,yy));
}

// License: MIT, author: Inigo Quilez, found: https://iquilezles.org/www/articles/distfunctions2d/distfunctions2d.htm
float triIso(vec2 p, vec2 q) {
  p.x = abs(p.x);
  vec2 a = p - q*clamp( dot(p,q)/dot(q,q), 0.0, 1.0 );
  vec2 b = p - q*vec2( clamp( p.x/q.x, 0.0, 1.0 ), 1.0 );
  float s = -sign( q.y );
  vec2 d = min( vec2( dot(a,a), s*(p.x*q.y-p.y*q.x) ),
                vec2( dot(b,b), s*(p.y-q.y)  ));
  return -sqrt(d.x)*sign(d.y);
}

// License: MIT, author: Inigo Quilez, found: https://iquilezles.org/articles/intersectors/
vec2 rayBox(vec3 ro, vec3 rd, vec3 boxSize, out vec3 outNormal)  {
  vec3 m = 1.0/rd; // can precompute if traversing a set of aligned boxes
  vec3 n = m*ro;   // can precompute if traversing a set of aligned boxes
  vec3 k = abs(m)*boxSize;
  vec3 t1 = -n - k;
  vec3 t2 = -n + k;
  float tN = max( max( t1.x, t1.y ), t1.z );
  float tF = min( min( t2.x, t2.y ), t2.z );
  if( tN>tF || tF<0.0) return vec2(-1.0); // no intersection
  outNormal = (tN>0.0) ? step(vec3(tN),t1)  : // ro ouside the box
                         step(t2,vec3(tF))  ;  // ro inside the box
  outNormal *= -sign(rd);
  return vec2( tN, tF );
}

vec3 sky(vec3 ro, vec3 rd) {
  vec3 col = vec3(0.0);
  col += sunColor/(1.0+0.00001 - dot(sunDir, rd));
  float hd = max(abs(rd.y+0.15), 0.00066);
  col += 100.0*glowColor0/sqrt(hd);
  col += glowColor2/(hd);
  return col;
}

// License: Unknown, author: Claude Brezinski, found: https://mathr.co.uk/blog/2017-09-06_approximating_hyperbolic_tangent.html
float tanh_approx(float x) {
  //  Found this somewhere on the interwebs
  //  return tanh(x);
  float x2 = x*x;
  return clamp(x*(27.0 + x2)/(27.0+9.0*x2), -1.0, 1.0);
}

vec3 glow(vec3 ro, vec3 rd, float beat) {
  vec3 bn;
  vec3 bro = ro;
  bro.y += -1000.0+70.0;
  vec2 bi = rayBox(bro, rd, vec3(90.0, 1000.0, 90.0), bn);
  float lightDist = distance(lightPos, ro);
  vec3 lightDir   = normalize(lightPos-ro);
  float g3        = 1.0+0.00001 - dot(lightDir, rd);
  vec3 col = vec3(0.0);
  col += 8.0*glowColor0/(g3);
  vec3 rrd = rd*transpose(roty)*ROTX(0.027);
  if (bi != vec2(-1.0)) {
    float bdi = tanh_approx(0.00125*(bi.y-bi.x));
    col += 1000.0*glowColor0*(bdi/max(rrd.y, 0.005));
  }
  
  float sx = abs(rrd.x);
  rrd.y += mix(0.00, 0.0125, beat)*(texture(iChannel0, vec2(0.5*sx, 0.75)).x-0.5);
  col += 20.0*glowColor0/(abs(mix(mix(20.0, 0.25, beat)*rrd.y*rrd.y, abs(rrd.y), tanh_approx(4.0*sx)))+mix(2.0, 0.5, beat)*sx*sx*sx+0.0001);

  col *= mix(1.0, 4.0, beat);
  return col;
}

vec3 side(vec3 col, vec3 ro, vec3 rd, vec3 nrd, float t, float nt, vec4 dim, vec3 c) {
  vec3 n = dim.xyz;

  vec3 p = ro + rd*t;
  vec3 np = ro + nrd*t;

  vec3 r = reflect(rd, n);
  vec3 ldiff = p - lightPos;
  vec3 ld = normalize(ldiff);
  vec3 rcol0 = sky(p, r);
  float dcol = max(dot(ld, n), 0.0);
  dcol *= dcol;
  float aa = distance(p, np);
  vec2 pp = planeCoord(p, c, vec3(0.0, 1.0, 0.0), dim);
  vec2 p0 = pp;
  vec2 p1 = pp;
  const vec2 tri =vec2(485, sqrt(3.0)*356.0);
  float d0 = triIso(p0, tri);
  float d1 = triIso(p1, 0.11*tri);
  float d = d0;
  vec3 bcol = col;
  float hf = smoothstep(-600.0, -400.0, p.y);
  vec3 pcol = 3.0*diffColor*dcol;  
  pcol += rcol0;
  pcol = mix(clamp(col, 0.0, 0.1), pcol, hf); 
  col = mix(col, pcol, smoothstep(aa, 0.0, d));
  col += topColor/max(0.00005*(d1-1.), 0.000025)*hf;
  return col;
}

vec3 pyramid(vec3 col, vec3 ro, vec3 rd, vec3 nrd) {
  const mat3 rotx = ROTX(radians(-51.8));
  const mat3 rr0  = rotx;
  const mat3 rr1  = rr0*ROTY(PI/2.0);

  const vec3 n0   = normalize(vec3(.0, 0.0, 1.0))*rr0;
  const vec3 c0   = vec3(0.0);
  const vec4 dim0 = vec4(n0, -dot(c0, n0));

  const vec3 n1   = normalize(vec3(.0, 0.0, 1.0))*rr1;
  const vec3 c1   = vec3(0.0);
  const vec4 dim1 = vec4(n1, -dot(c1, n1));

  float t0  = rayPlane(ro, rd , dim0);
  float nt0 = rayPlane(ro, nrd, dim0);
  float t1  = rayPlane(ro, rd , dim1);
  float nt1 = rayPlane(ro, nrd, dim1);
  if (t1 > 0.0 && nt1 > 0.0) {
    col = side(col, ro, rd, nrd, t1, nt1, dim1, c1);
  }
  if (t0 > 0.0 && nt0 > 0.0) {
    col = side(col, ro, rd, nrd, t0, nt0, dim0, c0);
  }


  return col;
}

vec3 render0(vec3 ro, vec3 rd, vec3 nrd, float beat) {
  const vec3 ro_ = vec3(0.0, 0.0, -2700.0)*roty;
  const mat3 rrd = ROTX(-0.2)*ROTY(0.); 
  rd *= rrd;
  ro = ro_;
  const float rdd = 3.0;
  const float mm = 4.0;

  vec3 glowCol = glow(ro, rd, beat);
  
  vec3 col = sky(ro, rd); 
  col = pyramid(col, ro, rd, nrd);
  col += glowCol;  
  col = clamp(col, 0.0, 10.0);
  return col;
}

#endif


float g_gd;

void warpWorld(inout vec3 p){
#if defined(WARP_WORLD)
  vec3 warp = path(p.z);
  vec3 dwarp = normalize(dpath(p.z));
  p.xy -= warp.xy;
  p -= dwarp*dot(vec3(p.xy, 0), dwarp)*0.5*vec3(1,1,-1);
#endif
}
// License: Unknown, author: Unknown, found: don't remember
float hash(float co) {
  return fract(sin(co*12.9898) * 13758.5453);
}

vec2 dfArcs(vec3 p) {
  vec3 p1 = p;
  float n1 = mod1(p1.z, 4.0);
  float h1 = hash(n1);
  float sh1 = -1.0+2.0*h1;
  vec3 p2 = p1;
  const mat2 rot = ROT(-PI*0.25);
  p1.xy *= ROT(iRot*sh1);
  p1.xy = abs(p1.xy);
  p1.xy *= rot;
  float d1 = cappedTorus(p1, SCA(PI*0.125*0.5), 3.0*vec2(1.0, 0.075)); 
  float d2 = flatTorus(p2, 3.0*vec2(1.0, 0.03));
  d2 = max(d2, -d1);
  return vec2(d1, d2);
}

float df(vec3 p, float t) {
  warpWorld(p);
  
  vec3 p0 = p;
  p0.y -= 3.0;
  p0.y = -p0.y;
  vec3 p1 = p;
  mod1(p1.z, 4.0); 
  p1 = p1.xzy;
  float d0 = arc(p0.xy, SCA(PI/6.0), 6.0, 0.8);
  vec2 dd1 = dfArcs(p);
  float d = d0;
  d = pmax(d, -(dd1.y-0.25), 0.125);
  d = min(d, dd1.x);
  d = min(d, dd1.y);
  float gd = dd1.x;
  t = max(t-MAX_RAY_LENGTH_HI*0.5, 0.0);
  g_gd = min(g_gd, gd+t*t*1E-4);
  
  return d;

}

float df(vec3 p) {
  return df(p, 0.0);
}
vec3 normal(vec3 pos) {
  vec2  eps = vec2(NORM_OFF,0.0);
  vec3 nor;
  nor.x = df(pos+eps.xyy) - df(pos-eps.xyy);
  nor.y = df(pos+eps.yxy) - df(pos-eps.yxy);
  nor.z = df(pos+eps.yyx) - df(pos-eps.yyx);
  return normalize(nor);
}

float rayMarchHi(vec3 ro, vec3 rd, float initt, out int iter) {
  float t = initt;
  const float tol = TOLERANCE_HI;
#if defined(BACKSTEP)
  vec2 dti = vec2(1e10,0.0);
#endif  
  int i = 0;
  for (i = 0; i < MAX_RAY_MARCHES_HI; ++i) {
    float d = df(ro + rd*t, t);
#if defined(BACKSTEP)
    if (d<dti.x) { dti=vec2(d,t); }
#endif  
    if (d < TOLERANCE_HI || t > MAX_RAY_LENGTH_HI) {
      break;
    }
    t += d;
  }
  
#if defined(BACKSTEP)
  if(i==MAX_RAY_MARCHES_HI) { t=dti.y; };
#endif
  iter = i;
  
  return t;
}


vec3 render1(vec3 ro, vec3 rd, vec3 nrd, vec2 sp) {
  float beat = beat();
  g_gd = 1E3;
  int iter;
  float t = rayMarchHi(ro, rd, 0.0, iter);
  float gd = g_gd;
  vec3 ggcol = (glowCol1)/(max(gd, 0.00125));
  vec3 skyCol = render0(ro, rd, nrd, beat);
  vec3 col = skyCol;

  float tt = t/MAX_RAY_LENGTH_HI;
  tt -= 0.33;
  tt = clamp(tt, 0.0, 1.0);
  float sfo = 1.0-exp(-9.0*tt*tt);

  if (t < MAX_RAY_LENGTH_HI) {
    vec3 p = ro+rd*t;
    vec3 n = normal(p);
    vec3 r = reflect(rd, n);
    vec3 nr = reflect(nrd, n);
    float fre0 = 1.0+dot(rd, n);
    float fre = fre0;
    fre *= fre;
    float dif = dot(sunDir, n); 
  
    float ao = 1.0-float(iter)/float(MAX_RAY_MARCHES_HI);
    float fo = mix(0.2, 0.5, ao);

    vec3 wp = p;
    warpWorld(wp);
    vec2 dd = dfArcs(wp);

    vec3 rcol = vec3(0.0);
    float hit = min(dd.x, dd.y);
#if defined(REFLECT)
    if (hit > .05) {
      g_gd = 1E3;
      int riter;
      float rt = rayMarchHi(p, r, 0.5,riter);
      float rgd = g_gd;
      vec3 rggcol = (glowCol1)/(max(rgd, 0.00125));
      rcol = clamp(rggcol, 0.0, 40.0);
      rcol *= smoothstep(0.66, 0.1, tt);
      if (rt < MAX_RAY_LENGTH_HI) {
        rcol += diffCol*0.2;
      } else {
        rcol += 0.5*render0(p, r, nr, beat);
      }
    } else {
    }
    rcol += 4.0*(diffCol+0.5)*glowCol1/max(dd.x*dd.x, 0.01);
#else
    rcol += 0.5*render0(p, r, nr);
    rcol += 4.0*(diffCol+0.5)*glowCol1/max(dd.x*dd.x, 0.01);
#endif
    col = vec3(0.0);
    col += sunCol*dif*dif*diffCol*fo;
    col += rcol*fre;
  }

  col = clamp(col, 0.0, 4.0);  
  col = mix(col, skyCol, sfo);

  col += clamp(ggcol, 0.0, 4.0);

#if defined(SKYBOX1)
  vec3 rrd = rd*transpose(roty)*ROTX(0.027);
  float flash = dot(rrd, normalize(vec3(0.0, -0.2, -1.0)))+1.0005;
  col += (0.01*vec3(0.5, 0.25, 1.0))*smoothstep(0.5, 1.0, beat)/flash;
#endif

return col;
}


vec3 effect(vec2 p, vec2 pp) {
  const vec3 up = normalize(vec3(0.0, 1.0, 0.0));

  float tm  = mod(TIME, 60.)-30.0;
  float z = 5.0*tm + iTravel;
#if defined(WARP_WORLD)
  
  vec3 ro = path(z);
  vec3 ww = normalize(dpath(z));
  vec3 dd = ddpath(z);
#else
  vec3 ro = vec3(0.0, 0.0, z);
  vec3 ww = normalize(vec3(0.0, 0.0, +1.0));
  vec3 dd = vec3(0.0);
#endif
  vec3 uu = normalize(cross(up+dd, ww));
  vec3 vv = (cross(ww,uu));
  // beat "punch": briefly narrow the FOV (zoom in) on kicks
  float fov = tan(TAU/6.)*(1.0 - 0.10*iBeat);
  vec2 np = p + 4.0/RESOLUTION.y;
  vec3 rd   = normalize(-p.x*uu + p.y*vv + fov*ww);
  vec3 nrd  = normalize(-np.x*uu + np.y*vv + fov*ww);

  vec3 col = render1(ro, rd, nrd, p);
  col -= 0.05*length(pp);

  col *= smoothstep(1.5, 0.5, length(pp));
  col = clamp(col, 0.0, 4.0); 
  col *= smoothstep(30.0, 28.0, abs(tm));
  return col;
}

void mainImage(out vec4 fragColor, in vec2 fragCoord) {
  vec2 q = fragCoord/RESOLUTION.xy;
  vec2 p = -1. + 2. * q;
  vec2 pp = p;
  p.x *= RESOLUTION.x/RESOLUTION.y;
  vec3 col = effect(p, pp);
  
  fragColor = vec4(col, 1.0);
}
